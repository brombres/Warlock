library AssetCompiler

class GenerateAssetConfig : Visitor
  PROPERTIES
    platform              : String
    writer                : PrintWriter
    unreferenced_graphics : String[]
    unreferenced_audio    : String[]
    unreferenced_data     : String[]

  METHODS
    method init( writer, unreferenced_graphics, unreferenced_audio, unreferenced_data )

    method append_remaining_unreferenced->Logical
      if (unreferenced_count == 0) return false

      if (current_target != "Global")
        writer.println
        writer.println "[Global]"
        current_target = "Global"
      endIf

      append_remaining( "Graphics", unreferenced_graphics )
      append_remaining( "Audio",    unreferenced_audio )
      append_remaining( "Data",     unreferenced_data )

      return true

    method append_remaining( folder:String, unreferenced:String[], &if_current_folder )->Logical
      if (unreferenced.is_empty) return false
      if (if_current_folder and current_folder != "Assets/$"(folder)) return false

      if (current_folder.filename != folder)
        writer.println
        writeln_echo "folder $"(folder)
        current_folder = File( "Assets" / folder )
        writeln_echo
      endIf

      forEach (filepath in unreferenced)
        if (filepath.ends_with(".png",&ignore_case) and filepath.contains("font",&ignore_case))
          local file = File( "Assets/$/$"(folder,filepath) )
          local bitmap = Bitmap( file )
          if (bitmap.error)
            throw Error( "Error loading image '$'. File is corrupt or invalid."(file) )
          endIf
          contingent
            necessary (bitmap.pixels[forEach in 0..<bitmap.width].alpha_byte == 255)
            writeln_echo "@ $ font height:$ width:variable"(filepath,bitmap.height-1)
          unsatisfied
            writeln_echo "@ $ font height:$ width:fixed"(filepath,bitmap.height)
          endContingent
        else
          if (filepath.ends_with(".font.json",&ignore_case))
            writeln_echo "- $ font_info"(filepath)
          else
            write_echo "@ $"(filepath)
            which (File(filepath).extension.to_lowercase)
              case "png", "jpg", "jpeg": writeln_echo " image"
              case "wav", "ima4":        writeln_echo " sound"
              case "mp3", "ogg":         writeln_echo " music"
              case "json":               writeln_echo " json"
              others:                    writeln_echo " binary"
            endWhich
          endIf
        endIf
      endForEach

      if (if_current_folder) writer.println

      unreferenced.clear

      return true

    method on_visit_node( cmd:Cmd )
      throw cmd.t.error( "TODO: GenerateAssetConfig.on_visit_node($)"(cmd.type_name) )

    method on_visit_node( cmd:Group )
      writer.print "group"
      if (cmd.multiline)
        writer.println
        forEach (filepath in cmd.filepaths)
          if (filepath->String.contains(' '))
            writer.println ''  "$"''(filepath->String.to_escaped_unicode(''"''))
          else
            writer.println ''  $''(filepath)
          endIf
        endForEach
        writer.println "endGroup"
      else
        writer.print ' '
        forEach (filepath at index in cmd.filepaths)
          if (index) writer.print ", "
          if (filepath->String.contains(' '))
            writer.print ''"$"''(filepath->String.to_escaped_unicode(''"''))
          else
            writer.print filepath
          endIf
        endForEach
        writer.println
      endIf

    method on_visit_node( cmd:AssetRule )
      writer.print cmd.filepath
      forEach (attribute as Attribute in cmd.attributes)
        writer.print ' '
        writer.print attribute.name
        if (attribute.value)
          writer.print ':'
          writer.print attribute.value
        endIf
      endForEach
      writer.println

    method on_visit_node( cmd:Comment )
      writer.println "#$"(cmd.content)

    method on_visit_node( cmd:EmptyLine )
      writer.println

    method on_visit_node( cmd:ExcludedAssetRule )
      writer.print "- "
      on_visit_node( cmd->(as AssetRule) )

    method on_visit_node( cmd:ExistingAssetRule )
      writer.print "@ "
      on_visit_node( cmd->(as AssetRule) )

    method on_visit_node( cmd:GenerateAssetRule )
      writer.print "+ "
      on_visit_node( cmd->(as AssetRule) )

    method on_visit_node( cmd:SetFolder )
      if (current_target == "Global")
        append_remaining( "Graphics", unreferenced_graphics, &if_current_folder )
        append_remaining( "Audio",    unreferenced_audio,    &if_current_folder )
        append_remaining( "Data",     unreferenced_data,     &if_current_folder )
      endIf

      prior.on_visit_node( cmd )
      writer.println "folder $"(cmd.path)

    method on_visit_node( cmd:SetTarget )
      if (current_target == "Global" and cmd.name != "Global")
        if (append_remaining_unreferenced) writer.println
      endIf

      prior.on_visit_node( cmd )
      writer.println "[$]"(cmd.name)

    method unreferenced_count->Int
      return unreferenced_graphics.count + unreferenced_audio.count + unreferenced_data.count

    method write_echo( value:String )
      print value
      writer.print value

    method writeln_echo
      println
      writer.println

    method writeln_echo( value:String )
      println value
      writer.println value
endClass
