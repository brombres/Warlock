module Warlock

enum PrimitiveType
  UNDEFINED,
  TRIANGLES,
  TEXTURED_TRIANGLES,
  LINES
endEnum

class Renderer [singleton]
  DEFINITIONS
    VERSION = 1

    TOTAL_COUNT_INDEX = 0
    VCOUNT_INDEX      = 2

  PROPERTIES
    render_data        = DataWriter()

    vertices           = Vertex[]
    primitive_type     : PrimitiveType
    texture            : Texture

    total_vertex_count : Int

  METHODS
    method init
      reset

    method reset
      primitive_type = PrimitiveType.UNDEFINED
      texture = null
      vertices.clear
      total_vertex_count = 0
      render_data.reset
      render_data.write_int32( VERSION )
      render_data.write_int32( 0 )  # total vertex count

    method draw( triangle:Triangle, color:Color )
      primitive_type = PrimitiveType.LINES
      local argb = color.argb
      vertices.add( Vertex(triangle.a,argb) )
      vertices.add( Vertex(triangle.b,argb) )
      vertices.add( Vertex(triangle.b,argb) )
      vertices.add( Vertex(triangle.c,argb) )
      vertices.add( Vertex(triangle.c,argb) )
      vertices.add( Vertex(triangle.a,argb) )

    method fill( triangle:Triangle, color:Color )
      primitive_type = PrimitiveType.TRIANGLES
      local argb = color.argb
      vertices.add( Vertex(triangle.a,argb,XY(0,0)) )
      vertices.add( Vertex(triangle.b,argb,XY(1,1)) )
      vertices.add( Vertex(triangle.c,argb,XY(0,1)) )

    method fill( triangle:Triangle, texture, uv:Triangle, color=Color.WHITE:Color )
      primitive_type = PrimitiveType.TEXTURED_TRIANGLES
      local argb = color.argb
      vertices.add( Vertex(triangle.a, argb, uv.a) )
      vertices.add( Vertex(triangle.b, argb, uv.b) )
      vertices.add( Vertex(triangle.c, argb, uv.c) )

    method finalized_render_data->Byte[]
      flush
      render_data.write_int32x( RenderCmd.END_DRAWING )
      render_data.seek( 4 )
      render_data.write_int32( total_vertex_count )
      return render_data.output_bytes

    method flush
      if (vertices.is_empty) return

      which (primitive_type)

        case TRIANGLES
          render_data.write_int32x( RenderCmd.DRAW_TRIANGLES )
          render_data.write_int32x( vertices.count / 3 )
          local data_count_pos = render_data.position
          render_data.write_int32( 0 )
          forEach (v in vertices)
            render_data.write_real32( v.x )
            render_data.write_real32( v.y )
            render_data.write_real32( v.z )
            render_data.write_int32( v.color )
          endForEach
          render_data.patch_int32( data_count_pos, (render_data.position - data_count_pos) - 4 )

        case TEXTURED_TRIANGLES
          render_data.write_int32x( RenderCmd.DRAW_TEXTURED_TRIANGLES )
          if (texture) render_data.write_int32x( texture.id )
          else         render_data.write_int32x( 0 )
          render_data.write_int32x( vertices.count / 3 )
          local data_count_pos = render_data.position
          render_data.write_int32( 0 )
          forEach (v in vertices)
            render_data.write_real32( v.x )
            render_data.write_real32( v.y )
            render_data.write_real32( v.z )
            render_data.write_int32( v.color )
            render_data.write_real32( v.u )
            render_data.write_real32( v.v )
          endForEach
          render_data.patch_int32( data_count_pos, (render_data.position - data_count_pos) - 4 )

        case LINES
          render_data.write_int32x( RenderCmd.DRAW_LINES )
          render_data.write_int32x( vertices.count / 2 )
          local data_count_pos = render_data.position
          render_data.write_int32( 0 )
          forEach (v in vertices)
            render_data.write_real32( v.x )
            render_data.write_real32( v.y )
            render_data.write_real32( v.z )
            render_data.write_int32( v.color )
          endForEach
          render_data.patch_int32( data_count_pos, (render_data.position - data_count_pos) - 4 )

      endWhich

      total_vertex_count += vertices.count
      vertices.clear

    method load_texture( id:Int, bitmap:Bitmap )
      flush
      render_data.write_int32x( RenderCmd.LOAD_TEXTURE )
      render_data.write_int32x( id )
      render_data.write_int32x( bitmap.width )
      render_data.write_int32x( bitmap.height )
      render_data.write_colors( bitmap.pixels )

    method set_primitive_type( new_type:PrimitiveType )
      if (new_type == @primitive_type) return
      flush
      @primitive_type = new_type

    method set_texture( new_texture:Texture )
      if (texture is new_texture) return
      flush
      @texture = new_texture
      if (new_texture) new_texture.load

endClass
