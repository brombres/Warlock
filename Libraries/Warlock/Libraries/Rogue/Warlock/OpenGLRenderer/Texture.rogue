module Warlock

augment Warlock::Texture
  NATIVE
    nativeHeader
    @|#ifndef GL_BGRA
     |  #ifdef GL_BGRA_EXT
     |    #ifndef __EMSCRIPTEN__
     |      #define GL_BGRA GL_BGRA_EXT
     |    #endif
     |  #endif
     |#endif

  PROPERTIES
    gl_id               : Int32
    gl_render_target_id : Int32

  METHODS
    method set_texture( bitmap:Bitmap, &preserve_bitmap )
      if (not bitmap) return

      if (not gl_id)
        native
        @|GLuint new_id;
         |glGenTextures( 1, &new_id );
         |$this->gl_id = (RogueInt32) new_id;
      endIf
      GLRenderer.log_errors( "generating texture id" )

      if (not gl_id) return

      native @|glBindTexture( GL_TEXTURE_2D, $this->gl_id );
      GLRenderer.log_errors( "binding texture" )

      native @|void* data = $bitmap->pixels->as_bytes;

      local w = bitmap.width
      local h = bitmap.height
      local handled = false

      native
      @|#if defined(GL_BGRA)
       |glTexImage2D( GL_TEXTURE_2D, 0, GL_RGBA, $w, $h, 0, GL_BGRA, GL_UNSIGNED_BYTE, data );
       |$handled = 1;
       |#endif

      if (not handled)
        bitmap.swap_red_and_blue
        native @|glTexImage2D( GL_TEXTURE_2D, 0, GL_RGBA, $w, $h, 0, GL_RGBA, GL_UNSIGNED_BYTE, data );
        if (preserve_bitmap) bitmap.swap_red_and_blue
      endIf

      GLRenderer.log_errors( "defining texture" )

endAugment
