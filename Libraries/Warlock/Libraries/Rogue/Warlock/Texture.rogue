module Warlock

class Texture
  PROPERTIES
    image_size       : XY       # In pixels; <= texture_size
    texture_size     : XY       # In pixels
    uv_size          : XY       # image_size/texture_sie; (1.0,1.0) or smaller
    is_opaque        : Logical
    is_loaded        : Logical

  METHODS
    method init( asset:Asset )
      asset.load( this=>on_loaded )

    method init( bitmap:Bitmap )
      set( bitmap )

    method on_loaded( asset:Asset )
      if (asset.error)
        set( null )
      else
        set( Bitmap(asset.bytes) )
      endIf

    method set( bitmap:Bitmap )
      if (not bitmap) bitmap = Bitmap(16,16).[ clear(Color.WHITE) ]

      local original_bitmap = bitmap

      is_opaque = bitmap.is_opaque
      if (not is_opaque)
        bitmap = bitmap.cloned
        bitmap.premultiply_alpha
      endIf

      image_size = bitmap.size

      local wp2 = bitmap.width.to_power_of_2
      local hp2 = bitmap.height.to_power_of_2
      if (wp2 != bitmap.width or hp2 != bitmap.height)
        if (bitmap is original_bitmap) bitmap = bitmap.cloned
        bitmap.expand_to_power_of_2
      endIf

      texture_size = bitmap.size
      uv_size = image_size / texture_size

      GLRenderer.set_texture( this, bitmap, &preserve_bitmap=(bitmap is original_bitmap) )

      is_loaded = true
endClass
